# Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
pkgname=ccminer-neoscrypt
pkgver=r1322.7353240
pkgrel=1
pkgdesc="Multiple algorythim miner for Nvidia GPUs"
arch=('x86_64')
url="https://github.com/KlausT/ccminer"
license=('GPL3')
depends=('curl' 'jansson' 'opencl-nvidia')
makedepends=('git' 'autoconf' 'cuda>=9.1' 'cuda<9.2')
options=('!emptydirs')
_commit=73532402987916e4f8fa58e44ef8154b15f1919f
source=("git+https://github.com/KlausT/ccminer.git#commit=${_commit}")
md5sums=('SKIP')

pkgver() {
	cd ccminer 
	
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd ccminer
	autoreconf -fiv
}

build() {
	cd ccminer
	CXXFLAGS="${CXXFLAGS} -std=c++11 -I/usr/include/openssl-1.0" \
        LDFLAGS='-L/usr/lib/openssl-1.0 -L/usr/lib' \
	./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib --with-cuda=/opt/cuda
	make
}

package() {
	cd ccminer
	install -m755 -D ccminer ${pkgdir}/usr/bin/ccminer-neoscrypt
}
