# Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
pkgname=ccminer-neoscrypt
pkgver=8.22
pkgrel=1
pkgdesc="Multiple algorythim miner for Nvidia GPUs"
arch=('x86_64')
url="https://github.com/KlausT/ccminer"
license=('GPL3')
depends=('curl' 'jansson' 'opencl-nvidia' 'gcc6-libs' 'cuda>=9.2' 'cuda<9.3')
makedepends=('git' 'autoconf' 'gcc6')
options=('!emptydirs')
source=("git+https://github.com/KlausT/ccminer.git#tag=${pkgver}")
md5sums=('SKIP')

prepare() {
	cd ccminer
	autoreconf -fiv
}

build() {
	cd ccminer
	CC=gcc-6 CXX=g++-6 \
	CFLAGS="${CFLAGS} -fPIC" CXXFLAGS="${CXXFLAGS} -fPIC" \
	./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib --with-cuda=/opt/cuda
	make
}

package() {
	cd ccminer
	install -m755 -D ccminer ${pkgdir}/usr/bin/ccminer-neoscrypt
}
