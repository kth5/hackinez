# Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
pkgname=ccminer-neoscrypt
pkgver=r1397.c7ddb61
pkgrel=1
pkgdesc="Multiple algorythim miner for Nvidia GPUs"
arch=('x86_64')
url="https://github.com/KlausT/ccminer"
license=('GPL3')
depends=('curl' 'jansson' 'opencl-nvidia' 'gcc6-libs')
makedepends=('git' 'autoconf' 'cuda>=9.1' 'cuda<9.2' 'gcc6')
options=('!emptydirs')
_commit=c7ddb61059eeea0ddacdad6228e5d5938fbfa1e8
source=("git+https://github.com/KlausT/ccminer.git#commit=${_commit}")
md5sums=('SKIP')

pkgver() {
	cd ccminer 
	
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd ccminer
	autoreconf -fiv
}

build() {
	cd ccminer
	CC=gcc-6 CXX=g++-6 \
	./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib --with-cuda=/opt/cuda
	make
}

package() {
	cd ccminer
	install -m755 -D ccminer ${pkgdir}/usr/bin/ccminer-neoscrypt
}
