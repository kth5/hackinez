# Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
pkgname=amdcovc
pkgver=r59.589c09c
pkgrel=1
pkgdesc="Simple commandline program to control AMD Overdrive settings"
arch=('x86_64')
url="https://github.com/matszpk/amdcovc"
license=('GPL3')
makedepends=('git' 'amd-adl-sdk')
options=('!emptydirs')
_commit=589c09cef00e8cc92258da2cd85089567e57735b
source=("git+https://github.com/matszpk/amdcovc.git#commit=${_commit}")
md5sums=('SKIP')

pkgver() {
	cd ${pkgname}
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd ${pkgname}
	sed 's@#include "../include/adl_sdk.h"@#include <ADL/adl_sdk.h>@g' -i amdcovc.cpp
}

build() {
	cd ${pkgname} 
	make
}

package() {
	cd ${pkgname} 
	install -m755 -D amdcovc ${pkgdir}/usr/bin/${pkgname}
}
