# Maintainer: Alexander Baldeck <alex.bldck@gmail.com>

# original AUR credits
# AUR Maintainer: Omar Pakker <omar.pakker@oracle.com>
pkgname=nevermore-miner
pkgver=r957.a937d7a
pkgrel=1
pkgdesc="Ravencoin miner for Nvidia GPUs"
arch=('x86_64')
url="https://github.com/brian112358/nevermore-miner"
license=('GPL3')
depends=('curl' 'jansson' 'opencl-nvidia' 'openssl-1.0' 'cuda>=9.2' 'cuda<9.3')
makedepends=('git' 'autoconf')
options=('!emptydirs')
_commit=a937d7a61eeb80456c4ad1c486b2312078cdbd72
source=("git+https://github.com/brian112358/nevermore-miner.git#commit=${_commit}")
md5sums=('SKIP')

pkgver() {
	cd ${pkgname}
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd ${pkgname}
	autoreconf -fiv
}

build() {
	cd ${pkgname}
	PATH=/opt/cuda/bin:$PATH
	./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib --with-cuda=/opt/cuda
	make
}

package() {
	cd ${pkgname}
	make DESTDIR=${pkgdir}/ install
	mv ${pkgdir}/usr/bin/ccminer ${pkgdir}/usr/bin/nevermore-miner
}
