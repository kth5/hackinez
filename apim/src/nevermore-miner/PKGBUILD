# Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
pkgname=nevermore-miner
pkgver=0.2.3
pkgrel=2
pkgdesc="Ravencoin miner for Nvidia GPUs"
arch=('x86_64')
url="https://github.com/brian112358/nevermore-miner"
license=('GPL3')
depends=('curl' 'jansson' 'opencl-nvidia' 'openssl-1.0' 'cuda>=10' 'cuda<11' 'gcc7-libs')
makedepends=('git' 'autoconf' 'gcc7')
options=('!emptydirs')
source=("git+https://github.com/brian112358/nevermore-miner.git#tag=v${pkgver}")
md5sums=('SKIP')

prepare() {
	cd ${pkgname}
	autoreconf -fiv
}

build() {
	cd ${pkgname}
	PATH=/opt/cuda/bin:$PATH
	CC=gcc-7 CXX=g++-7 \
	./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib --with-cuda=/opt/cuda
	make
}

package() {
	cd ${pkgname}
	make DESTDIR=${pkgdir}/ install
	mv ${pkgdir}/usr/bin/ccminer ${pkgdir}/usr/bin/nevermore-miner
}
