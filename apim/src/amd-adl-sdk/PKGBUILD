# Maintainer: Alexander Baldeck <alex.bldck@gmail.com>

# AUR Maintainer: Vi0L0 <vi0l093@gmail.com>
# Contributor: mid-kid <esteve.varela@gmail.com>
# Contributor: vchuravy (download  execution)

pkgname="amd-adl-sdk"
pkgver=10.2
pkgrel=1
pkgdesc="API to access display driver functionality for AMD graphics cards."
arch=('i686' 'x86_64')
url="http://developer.amd.com/tools-and-sdks/graphics-development/display-library-adl-sdk/"
license=('AMD-ADL')
install=${pkgname}.install
options=('!strip')
makedepends=('gcc' 'unzip')
_package=('ADL_SDK_V10.2')
source=(https://bassment.org/files/stuff/${_package}.zip)
md5sums=('a338b88c82c2870408cb27b878587e91')

prepare() {
    cd ${srcdir}/
    unzip ${_package}.zip

    sed -i -e '/include/a \#include <wchar.h>' ${srcdir}/include/adl_structures.h
    sed -i -e '/include/a \#include <stdbool.h>' ${srcdir}/include/adl_sdk.h
}

build() {
    cd ${srcdir}/adlutil
    gcc main.c -o adlutil -DLINUX -ldl -I ../include/
}

package() {
    install -m755 -d ${pkgdir}/usr/bin
    install -m755 ${srcdir}/adlutil/adlutil ${pkgdir}/usr/bin

    install -m755 -d ${pkgdir}/usr/include/ADL
    install -m644 ${srcdir}/include/* ${pkgdir}/usr/include/ADL

    install -m755 -d ${pkgdir}/usr/share/doc/ADL
    mv -v ${srcdir}/Public-Documents ${srcdir}/Sample ${srcdir}/Sample-Managed ${srcdir}/ADL_SDK.html ${pkgdir}/usr/share/doc/ADL
}
