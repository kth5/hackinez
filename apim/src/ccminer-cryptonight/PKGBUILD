# Maintainer: Alexander Baldeck <alex.bldck@gmail.com>

# original AUR credits
# AUR Maintainer: Omar Pakker <omar.pakker@oracle.com>
pkgname=ccminer-cryptonight
pkgver=r217.68623fa
pkgrel=1
pkgdesc="Multiple algorythim miner for Nvidia GPUs"
arch=('x86_64')
url="https://github.com/cryptonight/ccminer"
license=('GPL3')
depends=('curl' 'jansson' 'opencl-nvidia' 'openssl-1.0')
makedepends=('git' 'autoconf' 'cuda>=9.1' 'cuda<9.2')
options=('!emptydirs')
_commit=68623faae443f17431d2dfac0372b08f40eda723
source=("git+https://github.com/KlausT/ccminer-cryptonight.git#commit=${_commit}")
md5sums=('SKIP')

pkgver() {
	cd ${pkgname}
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd ${pkgname}
	autoreconf -fiv
}

build() {
	cd ${pkgname}
	PATH=/opt/cuda/bin:$PATH
	CFLAGS="-O3 -std=c++11" \
	CPPFLAGS='-I/usr/include/openssl-1.0' \
        LDFLAGS='-L/usr/lib/openssl-1.0 -L/usr/lib' \
	./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib --with-cuda=/opt/cuda
	make
}

package() {
	cd ${pkgname}
	install -m755 -D ccminer ${pkgdir}/usr/bin/ccminer-cryptonight
}
