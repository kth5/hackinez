# Maintainer: Alexander Baldeck <alex.bldck@gmail.com>

# original AUR credits
# AUR Maintainer: Omar Pakker <omar.pakker@oracle.com>
pkgname=ccminer-cryptonight
pkgver=3.05
pkgrel=1
pkgdesc="Multiple algorythim miner for Nvidia GPUs"
arch=('x86_64')
url="https://github.com/KlausT/ccminer-cryptonight"
license=('GPL3')
depends=('curl' 'jansson' 'opencl-nvidia' 'openssl-1.0' 'cuda>=9.2' 'cuda<9.3')
makedepends=('git' 'autoconf')
options=('!emptydirs')
source=("git+https://github.com/KlausT/ccminer-cryptonight.git#tag=${pkgver}")
md5sums=('SKIP')

prepare() {
	cd ${pkgname}
	autoreconf -fiv
}

build() {
	cd ${pkgname}
	PATH=/opt/cuda/bin:$PATH
	CFLAGS="-O3 -std=c++11" \
	CPPFLAGS='-I/usr/include/openssl-1.0' \
        LDFLAGS='-L/usr/lib/openssl-1.0 -L/usr/lib' \
	./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib --with-cuda=/opt/cuda
	make
}

package() {
	cd ${pkgname}
	install -m755 -D ccminer ${pkgdir}/usr/bin/ccminer-cryptonight
}
