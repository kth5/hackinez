# Maintainer: Alexander Baldeck <alex.bldck@gmail.com>

# original AUR credits
# AUR Maintainer: Omar Pakker <omar.pakker@oracle.com>
pkgname=ccminer-tpruvot
pkgver=r900.e1575c5
pkgrel=1
pkgdesc="Multiple algorythim miner for Nvidia GPUs"
arch=('x86_64')
url="https://github.com/tpruvot/ccminer"
license=('GPL3')
depends=('cuda' 'curl' 'jansson')
makedepends=('git' 'cuda' 'autoconf')
options=('!emptydirs')
_commit=e1575c5cb27a24016b9c5dd506d752d4c26d304e
source=("git+https://github.com/tpruvot/ccminer.git#commit=${_commit}"
	ccminer-openssl-1.1-api.patch
	ccminer-tpruvot-cuda9.patch)
md5sums=('SKIP'
	 '97c3594343589d2bee87696fa17505c4'
         '316a1b6e721b065c8a0027ce53c3c288')

pkgver() {
	cd ccminer
	
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd ccminer
	patch -Np0 -i ${srcdir}/ccminer-tpruvot-cuda9.patch
	patch -Np1 -i ${srcdir}/ccminer-openssl-1.1-api.patch
	autoreconf -fiv
}

build() {
	cd ccminer
        PATH=/opt/cuda/bin:$PATH
        CFLAGS="-O3 -std=c++11" \
        CPPFLAGS='-I/usr/include/openssl-1.0' \
        LDFLAGS='-L/usr/lib/openssl-1.0 -L/usr/lib' \
        ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib --with-cuda=/opt/cuda
	make
}

package() {
	cd ccminer
	install -m755 -D ccminer ${pkgdir}/usr/bin/${pkgname}
}
