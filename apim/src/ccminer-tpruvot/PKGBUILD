# Maintainer: Alexander Baldeck <alex.bldck@gmail.com>

# original AUR credits
# AUR Maintainer: Omar Pakker <omar.pakker@oracle.com>
pkgname=ccminer-tpruvot
pkgver=r920.30db7d2
pkgrel=1
pkgdesc="Multiple algorythim miner for Nvidia GPUs"
arch=('x86_64')
url="https://github.com/tpruvot/ccminer"
license=('GPL3')
depends=('curl' 'jansson' 'opencl-nvidia')
makedepends=('git' 'autoconf' 'cuda>=9' 'cuda<10')
options=('!emptydirs')
_commit=30db7d2433c2df880c05ba1fba736de4c05c88cc
source=("git+https://github.com/tpruvot/ccminer.git#commit=${_commit}")
md5sums=('SKIP')

pkgver() {
	cd ccminer
	
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd ccminer
	autoreconf -fiv
}

build() {
	cd ccminer
        PATH=/opt/cuda/bin:$PATH
	CFLAGS="-O3 -std=c++11" \
        ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib --with-cuda=/opt/cuda
	make
}

package() {
	cd ccminer
	install -m755 -D ccminer ${pkgdir}/usr/bin/${pkgname}
}
