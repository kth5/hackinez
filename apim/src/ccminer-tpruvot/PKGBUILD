# Maintainer: Alexander Baldeck <alex.bldck@gmail.com>

# original AUR credits
# AUR Maintainer: Omar Pakker <omar.pakker@oracle.com>
pkgname=ccminer-tpruvot
pkgver=2.2.6
pkgrel=1
pkgdesc="Multiple algorythim miner for Nvidia GPUs"
arch=('x86_64')
url="https://github.com/tpruvot/ccminer"
license=('GPL3')
depends=('curl' 'jansson' 'opencl-nvidia')
makedepends=('git' 'autoconf' 'cuda>=9.2' 'cuda<9.3')
options=('!emptydirs')
_commit=07859f93cef68072d8011f3f9a60d443ae11289e
source=("git+https://github.com/tpruvot/ccminer.git#commit=${_commit}")
md5sums=('SKIP')

prepare() {
	cd ccminer
	autoreconf -fiv
}

build() {
	cd ccminer
        PATH=/opt/cuda/bin:$PATH
	CFLAGS="-O3 -std=c++11" \
        ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib --with-cuda=/opt/cuda
	make
}

package() {
	cd ccminer
	install -m755 -D ccminer ${pkgdir}/usr/bin/${pkgname}
}
