# Maintainer: Alexander Baldeck <alex.bldck@gmail.com> 
pkgname=hodlminer-wolf
pkgver=r12.98ab9c7
pkgrel=1
_commit=98ab9c7aca8a11c2d1e5b25e5388c0d2a5b0f749
pkgdesc="Wolf's HOdl CPU miner"
arch=(x86_64)
url="https://github.com/Optiminer/hodlminer-wolf"
license=('GPL')
depends=('openssl' 'curl')
source=("git+https://github.com/Optiminer/hodlminer-wolf.git#commit=${_commit}")
md5sums=('SKIP')

pkgver() {
        cd ${pkgname} 
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd ${pkgname}
	sh autogen.sh
}

build() {
	cd ${pkgname}
	./configure
	make
}

package() {
	cd ${pkgname}
	install -D -m755 hodlminer ${pkgdir}/usr/bin/hodlminer-wolf
}
