# Maintainer: Alexander Baldeck <alex.bldck@gmail.com> 
pkgname=hodlminer-wolf
pkgver=r96.9155979
pkgrel=1
_commit=91559797c67e507fff9c756fe78b34f52f2fa184
pkgdesc="Wolf's HOdl CPU miner with mods"
arch=(x86_64)
url="https://github.com/kth5/hodlminer-wolf"
license=('GPL')
depends=('openssl' 'curl')
source=("git+https://github.com/kth5/hodlminer-wolf.git#commit=${_commit}")
md5sums=('SKIP')

pkgver() {
        cd ${pkgname} 
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd ${pkgname}
	sh autogen.sh
}

build() {
	cd ${pkgname}

	CFLAGS="${CFLAGS} -maes -msse4.2" \
	CXXFLAGS="${CXXFLAGS} -maes -msse4.2" \
	./configure --with-curl=/usr --with-crypto=/usr
	make
	mv hodlminer hodlminer-wolf

	make clean

        CFLAGS="${CFLAGS} -mavx -maes -msse4.2" \
        CXXFLAGS="${CXXFLAGS} -mavx -maes -msse4.2" \
        ./configure --with-curl=/usr --with-crypto=/usr
        make
        mv hodlminer hodlminer-wolf-avx

	make clean

        CFLAGS="${CFLAGS} -mavx2 -maes -msse4.2" \
        CXXFLAGS="${CXXFLAGS} -mavx2 -maes -msse4.2" \
        ./configure --with-curl=/usr --with-crypto=/usr
        make
        mv hodlminer hodlminer-wolf-avx2
}

package() {
	cd ${pkgname}
	install -D -m755 hodlminer-wolf ${pkgdir}/usr/bin/hodlminer-wolf
	install -D -m755 hodlminer-wolf-avx ${pkgdir}/usr/bin/hodlminer-wolf-avx
	install -D -m755 hodlminer-wolf-avx2 ${pkgdir}/usr/bin/hodlminer-wolf-avx2
}
